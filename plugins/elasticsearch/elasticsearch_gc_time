#!/usr/bin/env perl

# Parameters supported:
#
# config
# autoconf
#
# Magic markers:
#%# family=auto
#%# capabilities=autoconf

use strict;
use warnings;
use LWP;
use JSON qw/decode_json/;

=head1 NAME

elasticsearch_gc_time - A munin plugin that collects garbage collection time stats of your elasticsearch instances

=head1 APPLICABLE SYSTEMS

Elasticsearch

=head1 CONFIGURATION

None

=head1 BUGS

None known so far. If you find any, let me know.

=head1 AUTHOR

Timothy Messier (t0m) - c<< <tim.messier@gmail.com> >>
Kentaro Yoshida - https://github.com/y-ken
Rowan Wookey - https://www.rwky.net

=head1 LICENSE

MIT

=cut

my $url = exists $ENV{'url'} ? $ENV{'url'} : 'http://localhost:9200';

my $ua = LWP::UserAgent->new;
$ua->timeout(10);

sub autoconf {
    my $res  = $ua->get( $url, 'Content-Type' => 'application/json' );
    my $data = do {
        local $@;
        eval { decode_json( $res->content ) }
    };
    if ( $data->{version}->{number} ) {
        print("yes\n");
    }
    else {
        print("no\n");
    }
    exit(0);
}

if ( $ARGV[0] and $ARGV[0] eq 'autoconf' ) {
    autoconf();
}

sub get_json_from_url {
    my $uri = shift;
    my $res = $ua->get( $url . $uri, 'Content-Type' => 'application/json' );
    Carp::confess( $res->code . " for " . $uri ) unless $res->is_success;
    my $data = do {
        local $@;
        eval { decode_json( $res->content ) }
    };
    die( "Could not decode JSON from: " . $res->content ) unless $data;
    return $data;
}

my $data = get_json_from_url("/_nodes/_local/stats/jvm");
my %out  = ( young => 0, old => 0 );

foreach my $full_node_name ( keys %{ $data->{nodes} } ) {
    next unless $data->{nodes}{$full_node_name};
    if ( defined( $data->{nodes}{$full_node_name}{jvm}{gc}{collectors}{young} ) ) {
        $out{young}
          += $data->{nodes}{$full_node_name}{jvm}{gc}{collectors}{young}{collection_time_in_millis};
    }
    if ( defined( $data->{nodes}{$full_node_name}{jvm}{gc}{collectors}{old} ) ) {
        $out{old}
          += $data->{nodes}{$full_node_name}{jvm}{gc}{collectors}{old}{collection_time_in_millis};
    }
}
if ( $ARGV[0] and $ARGV[0] eq 'config' ) {
    print "graph_title Elasticsearch gc time\n";
    print "graph_category elasticsearch\n";
    print "graph_vlabel miliseconds\n";

    foreach my $name ( keys %out ) {
        print "$name.label $name\n" . "$name.type COUNTER\n";
    }
}
else {
    foreach my $name ( keys %out ) {
        print "$name.value " . $out{$name} . "\n";
    }
}
