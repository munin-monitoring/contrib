#!/usr/bin/perl

use strict;
use warnings;
use Munin::Plugin::SNMP;
use vars qw($DEBUG);

$DEBUG = $ENV{'MUNIN_DEBUG'};

my %oids = (
        cifsConnectedUsers	=> 'ConnectedUsers',
        cifsNSessions		=> 'Sesions',
        cifsNOpenFiles		=> 'OpenFiles',
);

if (defined $ARGV[0] and $ARGV[0] eq 'snmpconf') {
    print "require 1.3.6.1.4.1.789.1.7.2.1.0 [0-9]\n";
    exit 0;
}

my $session = Munin::Plugin::SNMP->session();

if (defined $ARGV[0] and $ARGV[0] eq "config") {

    my ($host, undef, undef, undef) = Munin::Plugin::SNMP->config_session();

    print "host_name $host\n" unless $host eq 'localhost';
    print "graph_title $host CIFS sessions\n";
    print "graph_args --base 1000 --lower-limit 0 --rigid\n";
    print "graph_vlabel CIFS\n";
    print "graph_category CIFS\n";
    print "graph_info This graph shows CIFS sessions status for the $host.\n";
    print "graph_order ";

    foreach (sort keys %oids) { print "$_ "; }
    print "\n";

    foreach my $k (sort keys %oids) {
        print "$k.info The number of CIFS $oids{$k}.\n";
        print "$k.label $oids{$k}\n";
        print "$k.min 0\n";
        print "$k.type GAUGE\n";
    }

    exit 0;
}

my $values = $session->get_hash(
    -baseoid => '1.3.6.1.4.1.789.1.7.2',
    -cols    => {
	9 => 'cifsConnectedUsers',
	12 => 'cifsNSessions',
	13 => 'cifsNOpenFiles',
    },
);

foreach my $k (keys %oids) {
    my $v = 'U';
    $v = $values->{0}->{$k} if (defined $values);
    print "$k.value $v\n";
}

exit 0;

__END__
